// 引入 wifi 模块，并实例化，不同的芯片这里的依赖可能不同
#include <ESP8266WiFi.h>
static WiFiClient espClient;

// 引入阿里云 IoT SDK
#include <AliyunIoTSDK.h>

// 设置产品和设备的信息，从阿里云设备信息里查看
#define PRODUCT_KEY ""
#define DEVICE_NAME ""
#define DEVICE_SECRET ""
#define REGION_ID ""

// 设置 wifi 信息
#define WIFI_SSID ""
#define WIFI_PASSWD ""

// 本案例使用esp-01s，引脚为 0 2
#define OUTPUT_PIN 0
#define STATUS_LED_PIN 2

void setup()
{
    Serial.begin(115200);

    // 初始化引脚
    pinMode(STATUS_LED_PIN, OUTPUT);
    pinMode(OUTPUT_PIN, OUTPUT);

    // 指示灯
    digitalWrite(STATUS_LED_PIN, LOW);
    
    // 初始化 wifi
    wifiInit(WIFI_SSID, WIFI_PASSWD);
    
    // 初始化 iot，需传入 wifi 的 client，和设备产品信息
    AliyunIoTSDK::begin(espClient, PRODUCT_KEY, DEVICE_NAME, DEVICE_SECRET, REGION_ID);
    
    // 绑定一个设备属性回调，当远程修改此属性，会触发 powerCallback
    // PowerSwitch 是在设备产品中定义的物联网模型的 id
    AliyunIoTSDK::bindData("PowerSwitch", powerCallback);
    
    // 发送一个数据到云平台，LightLuminance 是在设备产品中定义的物联网模型的 id
    AliyunIoTSDK::send("LightLuminance", 100);
}

void loop()
{
    AliyunIoTSDK::loop();
}

// 初始化 wifi 连接
void wifiInit(const char *ssid, const char *passphrase)
{
    WiFi.mode(WIFI_STA);
    WiFi.begin(ssid, passphrase);
    while (WiFi.status() != WL_CONNECTED)
    {
        delay(1000);
        Serial.println("WiFi not Connect");
    }
    Serial.println("Connected to AP");
}

// 电源属性修改的回调函数
void powerCallback(JsonVariant p)
{
    int PowerSwitch = p["PowerSwitch"];
    if (PowerSwitch == 1)
    {
        // 启动设备
        digitalWrite(OUTPUT_PIN, HIGH);
        digitalWrite(STATUS_LED_PIN, HIGH);
    } else {
        digitalWrite(OUTPUT_PIN, LOW);
        digitalWrite(STATUS_LED_PIN, LOW);
    }
}
